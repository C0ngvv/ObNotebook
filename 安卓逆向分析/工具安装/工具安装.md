## Apktool
官方安装指导链接：[Install Guide | Apktool](https://apktool.org/docs/install)

Windows版本：

![](images/Pasted%20image%2020230829105540.png)

创建一个目录如`D:\DevelopmentKit\apktool`作为apktool工具目录，然后下载wrapper script到这个目录，保存文件名为`apktool.bat`。这个文件国内可能打不开，里面的内容如下，直接创建bat文件拷贝即可。

```shell
@echo off
setlocal
set BASENAME=apktool_
chcp 65001 2>nul >nul

set java_exe=java.exe

if defined JAVA_HOME (
set "java_exe=%JAVA_HOME%\bin\java.exe"
)

rem Find the highest version .jar available in the same directory as the script
setlocal EnableDelayedExpansion
pushd "%~dp0"
if exist apktool.jar (
    set BASENAME=apktool
    goto skipversioned
)
set max=0
for /f "tokens=1* delims=-_.0" %%A in ('dir /b /a-d %BASENAME%*.jar') do if %%~B gtr !max! set max=%%~nB
:skipversioned
popd
setlocal DisableDelayedExpansion

rem Find out if the commandline is a parameterless .jar or directory, for fast unpack/repack
if "%~1"=="" goto load
if not "%~2"=="" goto load
set ATTR=%~a1
if "%ATTR:~0,1%"=="d" (
    rem Directory, rebuild
    set fastCommand=b
)
if "%ATTR:~0,1%"=="-" if "%~x1"==".apk" (
    rem APK file, unpack
    set fastCommand=d
)

:load
"%java_exe%" -jar -Xmx1024M -Duser.language=en -Dfile.encoding=UTF8 -Djdk.util.zip.disableZip64ExtraFieldValidation=true -Djdk.nio.zipfs.allowDotZipEntry=true "%~dp0%BASENAME%%max%.jar" %fastCommand% %*

rem Pause when ran non interactively
for /f "tokens=2" %%# in ("%cmdcmdline%") do if /i "%%#" equ "/c" pause
```

然后下载apttool的jar文件到相同目录，这个可直接下载：[iBotPeaches / Apktool / Downloads — Bitbucket](https://bitbucket.org/iBotPeaches/apktool/downloads/)

![](images/Pasted%20image%2020230829105922.png)

最好将目录（`D:\DevelopmentKit\apktool`）添加到环境变量中即可，打开DoS窗口就可以运行。

![](images/Pasted%20image%2020230829110114.png)

